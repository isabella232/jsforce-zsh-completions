#compdef jsforce-deploy

function _jsforce-deploy() {
  local context curcontext=$curcontext state line
  typeset -A opt_args
  local ret=1

  _arguments -C \
    '(-h --help)'{-h,--help}'[Output usage information]' \
    '(-V --version)'{-V,--version}'[Output the version number]' \
    '(-c --connection -u --username -p --password -l --loginUrl --sandbox)'{-c,--connection}'[Connection name stored in connection registry]: :__connections' \
    '(-u --username -c --connection)'{-u,--username}'[Salesforce username]: :' \
    '(-p --password -c --connection)'{-p,--password}'[Salesforce password (and security token, if available)]: :' \
    '(-l --loginUrl --sandbox -c --connection)'{-l,--loginUrl}'[<loginUrl> Salesforce login url]: :((production\:"Use production login server (login.salesforce.com)" sandbox\:"Using sandbox login server (test.salesforce.com)"))' \
    '(--sandbox -l --loginUrl -c --connection)--sandbox[Login to Salesforce sandbox]' \
    '(-D --directory -Z --zipFile --pid)'{-D,--directory}'[Local directory path of the package to deploy]: :_files -/' \
    '(-Z --zipFile -D --directory --pid)'{-Z,--zipFile}'[Input file path of ZIP archive of metadata files to deploy]: :_files' \
    '(--dry-run --checkOnly --pid)--dry-run[Dry run. Same as --checkOnly]' \
    '(--checkOnly --dry-run --pid)--checkOnly[Whether Apex classes and triggers are saved to the organization as part of the deployment]' \
    '(--testLevel --pid)--testLevel[Specifies which tests are run as part of a deployment]: :((NoTestRun RunSpecifiedTests RunLocalTests RunAllTestsInOrg))' \
    '(--runTests --pid)--runTests[A list of Apex tests to run during deployment (commma separated)]: :' \
    '(--ignoreWarnings --pid)--ignoreWarnings[Indicates whether a warning should allow a deployment to complete successfully (true) or not (false)]' \
    '(--rollbackOnError --pid)--rollbackOnError[Indicates whether any failure causes a complete rollback (true) or not (false)]' \
    '(--pid -D --directory -Z --zipFile --dry-run --checkOnly --testLevel --runTests --ignoreWarnings --rollbackOnError --pollTimeout --pollInterval)--pid[Process ID of previous deployment to check status]: :' \
    '(--pollTimeout --pid)--pollTimeout[Polling timeout in millisec (default is 60000ms)]: :' \
    '(--pollInterval --pid)--pollInterval[Polling interval in millisec (default is 5000ms)]: :' \
    '(--verbose)--verbose[Output execution detail log]' \
    '(-)*:arguments:->args' \
    && ret=0

  case $state in
    (args)
      ;;
  esac

  return ret
}

__connections() {
  local -a _c
  local _config_path="${HOME}/.jsforce/config.json"
  _c=(
    ${(@f)"$(cat "$_config_path" | jq -r '(.connections | keys)[]')"}
  )
  _describe -t commands connections _c
}

_jsforce-deploy "$@"
